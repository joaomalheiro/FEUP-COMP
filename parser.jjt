options {
    MULTI=true;
    LOOKAHEAD=1;
}

PARSER_BEGIN(JmmParser)
// código Java que invoca o parser
public class JmmParser {
    public static void main(String args[]) throws ParseException {
        // criação do objecto utilizando o constructor com argumento para
        // ler do standard input (teclado)
        try {
            java.io.FileInputStream file = new java.io.FileInputStream("test-files/" + args[0]);
            JmmParser parser = new JmmParser(file);
            SymbolTable classTable = new SymbolTable();
            SimpleNode root = parser.program(classTable);
            root.dump("");

            CodeGenerator codeGen = new CodeGenerator(root);
            codeGen.generate();
            
        } catch (java.io.FileNotFoundException e) {
            System.out.println(e);
        }
    }
}
PARSER_END(JmmParser)
// símbolos que não devem ser considerados na análise 
SKIP :
{
    " " 
    | "\t" 
    | "\r" 
    | "\n"
    | < "//"(~["\r","\n"])* >
    | < "/*" > : MLCOMMENT
}
//SKIP multi-lined comments
<MLCOMMENT> SKIP :
{
  < "*/" > : DEFAULT
| < ~[] >   
}
//global variable for error count
TOKEN_MGR_DECLS : {
    int errors = 0;
}
// definição dos tokens (símbolos terminais)
TOKEN :
{
    < INTEGER : (["0" - "9"])+ >
    | < CLASS : "class" >
    | < EXTENDS : "extends" >
    | < LEFTBRACKET : "{" >
    | < RIGHTBRACKET : "}" >
    | < LEFTPARENTHESES : "(" >
    | < RIGHTPARENTHESES : ")" >
    | < LEFTSQUAREBRACKET : "[" >
    | < RIGHTSQUAREBRACKET : "]" >
    | < INT : "int" >
    | < BOOLEAN : "boolean" >
    | < STRINGARRAY : "String[]" >
    | < VOID : "void" >
    | < COLON : "," >
    | < DOT : "." >
    | < SEMICOLON : ";" >
    | < PUBLIC : "public" >
    | < STATIC : "static" >
    | < MAIN : "main" >
    | < IF : "if" >    
    | < ELSE : "else" >      
    | < WHILE : "while" >  
    | < RETURN: "return" >
    | < EQUALS: "=" >    
    | < COMMERCIALE: "&&" >
    | < MINOR: "<" >
    | < ADDSUB: "+" | "-" >
    | < MULDIV: "*" | "/" >
    | < LENGTH: "length" >
    | < TRUE: "true" >
    | < FALSE: "false" >
    | < THIS: "this" >
    | < NEW: "new" >
    | < EXCLAMATION: "!" >
    | < IDENTIFIER : (["a" - "z"] | ["A" - "Z"])+ (["a" - "z"] | ["A" - "Z"] | ["0" - "9"] | "_")* >
}
// definição da produção
SimpleNode program(SymbolTable classTable) : {}
{
    classDeclaration(classTable) <EOF> {return jjtThis;}
}

void classDeclaration(SymbolTable classTable) : {Token t, t1;}
{
    <CLASS> t=<IDENTIFIER> {jjtThis.name = t.image;}#IDENTIFIER [t1=<EXTENDS> (t=<IDENTIFIER> {jjtThis.name = t.image;}#IDENTIFIER) {jjtThis.index = t1.kind;}#EXTENDS(1)]
    <LEFTBRACKET> 
        (varDeclaration(classTable, Symbol.Access.global))*
        (t=<PUBLIC> (mainDeclaration(classTable) | methodDeclaration()) {jjtThis.index = t.kind;}#PUBLIC(1))*
    <RIGHTBRACKET>
}

void mainDeclaration(SymbolTable classTable) : {Token t; Symbol mainMethod;} 
{
    {mainMethod = new Symbol(Symbol.Access.global);}
    t=<STATIC> {jjtThis.index = t.kind;}#STATIC t=<VOID> {jjtThis.index = t.kind;}#VOID <MAIN> <LEFTPARENTHESES> t=<STRINGARRAY> {jjtThis.index = t.kind;}#STRINGARRAY t=<IDENTIFIER> {jjtThis.name = t.image;}#IDENTIFIER(1) <RIGHTPARENTHESES>
    <LEFTBRACKET>
        methodBody()
    <RIGHTBRACKET>
}

void varDeclaration(SymbolTable table, Symbol.Access access) : {Token t; Symbol var;}
{
    {var = new Symbol(access);}
    type(var) t=<IDENTIFIER> {jjtThis.name = t.image; var.setIdentifier(t.image);}#IDENTIFIER <SEMICOLON>
    {table.addSymbol(var);} 
}

void methodDeclaration() : {Token t; Symbol method;}
{
    {method = new Symbol(Symbol.Access.global);}
    type(method) t=<IDENTIFIER> {jjtThis.name = t.image;}#IDENTIFIER <LEFTPARENTHESES> (parameterDeclaration() ( <COLON> parameterDeclaration())*)? <RIGHTPARENTHESES>
    <LEFTBRACKET>
        methodBody()
        t=<RETURN> expression() {jjtThis.index = t.kind;}#RETURN(1) <SEMICOLON>
    <RIGHTBRACKET>
}

void methodBody() : {SymbolTable methodTable;}
{
    {methodTable = new SymbolTable();}
    (LOOKAHEAD(2) varDeclaration(methodTable, Symbol.Access.local))*
    (statement())*
}

void parameterDeclaration() : {Token t; Symbol param;}
{
    {param = new Symbol(Symbol.Access.parameter);}
    type(param) t=<IDENTIFIER> {jjtThis.name = t.image;}#IDENTIFIER
}

void type(Symbol symbol) #void: {Token t;}
{
    (t=<INT> {jjtThis.index = t.kind;}#INT {symbol.setType("int");} [t=<LEFTSQUAREBRACKET> {jjtThis.index = t.kind;}#LEFTSQUAREBRACKET t=<RIGHTSQUAREBRACKET> {jjtThis.index = t.kind;}#RIGHTSQUAREBRACKET {symbol.setType("int[]");}]) | 
    (t=<BOOLEAN> {jjtThis.index = t.kind;}#BOOLEAN {symbol.setType("boolean");}) | 
    (t=<IDENTIFIER> {jjtThis.name = t.image;}#IDENTIFIER {symbol.setType(t.image);})
}

void statement() #void: {Token t;}
{
    (<LEFTBRACKET> (statement())* <RIGHTBRACKET>) |   
    (t=<IF> condition() body() {jjtThis.index = t.kind;}#IF(2) t=<ELSE> body() {jjtThis.index = t.kind;}#ELSE(1)) |
    (whileStatement()) |
    (expression() <SEMICOLON>)
}

void whileStatement() #void: {Token t, t1;}
{
    t=<WHILE>
    try {
        condition()
    }
    catch (ParseException e) {
        System.out.println(e.toString());
        {token_source.errors++; if(token_source.errors==10){System.out.println("10 errors found, stopping program!"); System.exit(-1);}}
        do {
            t1 = getNextToken();
        } while (t1.kind != RIGHTPARENTHESES);
    }
    body()
    {jjtThis.index = t.kind;}#WHILE(2)
}

void condition() : {}
{
    <LEFTPARENTHESES> expression() <RIGHTPARENTHESES>
}

void body() : {}
{
    statement()
}

void expression() #void: {Token t;}
{
    expression2() [t=<EQUALS> expression2() {jjtThis.index = t.kind;}#EQUALS(2)]
}

void expression2() #void: {Token t;}
{
    expression3() (
        t=<COMMERCIALE> expression3() {jjtThis.index = t.kind;}#COMMERCIALE(2)
    )*
}

void expression3() #void: {Token t;}
{
    expression4() (
        t=<MINOR> expression4() {jjtThis.index = t.kind;}#MINOR(2)
    )*
}

void expression4() #void: {Token t;}                        
{
    expression5() (
        t=<ADDSUB> expression5() {jjtThis.name = t.image;}#ADDSUB(2)
    )*
}

void expression5() #void: {Token t;}                        
{
    expression6() (
        t=<MULDIV> expression6() {jjtThis.name = t.image;}#MULDIV(2)
    )*
}

void expression6() #void: {}                        
{
    expression7() (
        array() #array(2)
    )*
}

void expression7() #void: {Token t;}                             
{
    expressionfinal() (
        t=<DOT> (
            (t=<LENGTH> {jjtThis.index = t.kind;}#LENGTH) | 
            (functionCall()) 
        ) 
        {jjtThis.index = t.kind;}#DOT(2)
    )*
}

void functionCall() : {Token t;}
{
    t=<IDENTIFIER> {jjtThis.name = t.image;}#IDENTIFIER <LEFTPARENTHESES> (parameters())? <RIGHTPARENTHESES>
}

void parameters() : {}
{
    expression() (<COLON> expression())*
}

void expressionfinal() #void: {Token t, t1;}                                            
{
    (t=<INTEGER> {jjtThis.name = t.image;}#INTEGER)|
    (t=<TRUE> {jjtThis.index = t.kind;}#TRUE) |
    (t=<FALSE> {jjtThis.index = t.kind;}#FALSE) |
    (t=<IDENTIFIER> {jjtThis.name = t.image;}#IDENTIFIER) |
    (t=<THIS> {jjtThis.index = t.kind;}#THIS) |
    (t=<EXCLAMATION> expressionfinal() {jjtThis.index = t.kind;}#EXCLAMATION) |
    (<LEFTPARENTHESES> expression() <RIGHTPARENTHESES>) |
    (t1=<NEW> (((t=<INT> {jjtThis.index = t.kind;}#INT) array() #array(2) {jjtThis.index = t1.kind;}#NEW(1)) | ((t=<IDENTIFIER> {jjtThis.name = t.image;}#IDENTIFIER) {jjtThis.index = t1.kind;}#NEW(1) <LEFTPARENTHESES> <RIGHTPARENTHESES>) ))
}

void array() #void: {}
{
    <LEFTSQUAREBRACKET> expression() <RIGHTSQUAREBRACKET>
}